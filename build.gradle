plugins {
    id 'java-library'
    id "maven-publish"
    id "org.openmicroscopy.dsl" version "5.5.0-SNAPSHOT"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name 'ome.maven'
        url 'https://artifacts.openmicroscopy.org/artifactory/maven/'
    }
    maven {
        name 'unidata-releases'
        url "https://artifacts.unidata.ucar.edu/repository/unidata-releases"
    }
}

group = 'org.openmicroscopy'
version = '5.5.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.testng', name: 'testng', version: '6.14.2'

    implementation 'edu.ucar:bufr:3.0'
    implementation 'edu.ucar:udunits:4.5.5'
    implementation 'edu.ucar:netcdf:4.3.22'

    // Needs updating to later versions
    api 'org.springframework:spring-orm:4.3.14.RELEASE'
    api 'org.springframework:spring-context:4.3.14.RELEASE'
    api 'org.hibernate:hibernate-core:3.6.10.Final'
    api 'org.hibernate:hibernate-search:3.4.2.Final'

    // Our libraries
    api 'org.openmicroscopy:ome-xml:5.6.2'
    api 'ome:formats-bsd:5.9.2'
    api 'ome:formats-gpl:5.9.2'
}

test {
    // enable TestNG support (default is JUnit)
    useTestNG() {
        suites "src/test/resources/unit.testng.xml"
    }
}

sourceSets {
    main {
        java {
            srcDirs 'src/generated/java'
        }
        resources {
            srcDirs 'src/generated/resources'
        }
    }
}

dsl {
    omeXmlFiles fileTree(
            dir: "src/main/resources/mappings",
            include: '**/*.ome.xml'
    )
    templateFiles fileTree(
            dir: "src/main/resources/templates",
            include: "**/*.vm"
    )
    outputPath "src/generated"

    code {
        java {
            template "object.vm"
            outputPath "java"
            formatOutput { st ->
                "${st.package.replace('.', '/')}/${st.shortname}.java"
            }
        }
    }

    resource {
        hibernate {
            template "cfg.vm"
            outputFile "resources/hibernate.cfg.xml"
        }

        sql {
            template "views.vm"
            outputFile "resources/views.sql"
        }

        enums {
            template "enums.vm"
            outputFile "resources/enums.properties"
        }
    }
}

clean {
    delete "src/generated"
}

// Add publish functionality
apply from: 'publish.gradle'